ACTOR RandomZombieMan : RandomSpawner 3004
{
	DropItem "FT_ZombieMan"
	DropItem "SentryGunMan"
	DropItem "IceZombieMan"
	DropItem "ZombieMan_Translation1"
	DropItem "ZombieMan_Translation2"
}
ACTOR RandomChaingunGuy : RandomSpawner 65
{
	DropItem "FT_ChaingunGuy"
	DropItem "VileGunGuy"
	DropItem "IceChaingunGuy"
	DropItem "ChaingunGuy_Translation1"
	DropItem "ChaingunGuy_Translation2"
}
ACTOR RandomDemon : RandomSpawner 3002
{
	DropItem "FT_Demon"
	DropItem "IceDemon"
	DropItem "Demon_Translation1"
	DropItem "Demon_Translation2"
}
ACTOR RandomDoomImp : RandomSpawner 3001
{
	DropItem "FT_DoomImp"
	DropItem "IceDoomImp"
	DropItem "DoomImp_Translation1"
	DropItem "DoomImp_Translation2"
}
ACTOR RandomArchvile : RandomSpawner 64
{
	DropItem "FT_Archvile"
	DropItem "RaisingArchvile"
	DropItem "BloodArchvile"
}
ACTOR RandomCyberdemon : RandomSpawner 16
{
	DropItem "FT_Cyberdemon"
	DropItem "RaisingCyberdemon"
	DropItem "MiniCyberdemon"
}
ACTOR SentryGunMan : ZombieMan
{
	Health 200 // 100HP - броня
	Mass 115
	Speed 11
	PainChance 32
	MONSTER
	+MISSILEMORE
	+MISSILEEVENMORE
	+CANPUSHWALLS
	+CANUSEWALLS
	+ACTIVATEIMPACT
	DropItem CrateAmmo 157
	DropItem CrateAmmo2 12
	DropItem AntiTechgrenade 122
	AttackSound "EngineerHandGun"
	Obituary "%o was gunned down by engineer."
	States
	{
	Spawn:   
		GO99 E 0 A_Jump(25, "Missile2")
		GO99 AB 15 A_Look
		Loop
	See:
		TNT1 A 0 A_FastChase
		TNT1 A 0 A_JumpIfInventory("Appeared",1, "See2")
		TNT1 A 0 A_Jump(43,"See3")
		TNT1 A 0 A_GiveInventory("Appeared",1)
		Goto See2
	See3:
		GO99 A 1 A_GiveInventory("Appeared",1)
        Goto GotARocketIntoAFACE	
	See2:
		GO99 A 2 ThrustThingZ(0,33,4,0) 
		GO99 AAAABBBB 1 A_FastChase
		GO99 CCCCDDDD 1 A_FastChase
		GO99 BBBBAAAA 1 A_FastChase
		TNT1 A 0 A_Jump(127, "JumpForward", "JumpBackward")
		Loop
	JumpForWard:
		GO99 A 0 A_Recoil(0.554) 
		GO99 A 0 ThrustThingZ(0,33,4,0)
		GO99 A 0 A_Recoil(0.554)
		GO99 A 1 A_Recoil(0.554)
		Goto See2
	JumpBackward:
		GO99 A 0 A_Recoil(-0.554)
		GO99 A 0 ThrustThingZ(1,33,5,0)
		GO99 A 0 A_Recoil(-0.554)
		GO99 A 1 A_Recoil(-0.554)
		Goto See2
	Pain:
		GO99 A 3 A_Pain
		Goto See2
	Missile:
		GO99 E 0 A_Jump(45, "Missile2", "Missile3", "Missile4")
		GO99 E 10 A_FaceTarget
		GO99 F 8 A_PosAttack
		GO99 E 8
		Goto See2
	GotARocketIntoAFACE:
	    TNT1 A 0 A_Jump(255, "Missile2", "Missile3", "Missile4")
	Missile2:
		GO99 E 5 A_FACETARGET 
		GO99 E 0 A_PlaySound("vox/rus/turretmode/default")
		GO99 E 0 Bright A_SpawnItemEx ("EnemySentryGun", 56, 0, 8, 0, 0, 0, 0, 1)
		GO99 E 25 A_FACETARGET 
		Goto See2
	Missile3:
		GO99 E 5 A_FACETARGET 
		GO99 E 0 A_PlaySound("vox/rus/turretmode/rocket")
		GO99 E 0 Bright A_SpawnItemEx ("EnemyRocketSentryGun", 56, 0, 8, 0, 0, 0, 0, 1)
		GO99 E 25 A_FACETARGET 
		Goto See2
	Missile4:
		GO99 E 5 A_FACETARGET 
		GO99 E 0 A_PlaySound("vox/rus/turretmode/plasma")
		GO99 E 0 Bright A_SpawnItemEx ("EnemyPlasmaSentryGun", 56, 0, 8, 0, 0, 0, 0, 1)
		GO99 E 25 A_FACETARGET 
		Goto See2
	Death:
		GO99 H 7 A_CustomMissile("electricgrenade",42,0,random(0,360))
		GO99 I 7 A_UnHideThing
		GO99 J 7 A_NoBlocking
		GO99 KLM 7
		GO99 N -1
		Stop
	XDeath:
		GO99 O 5
		GO99 P 5 A_XScream
		GO99 Q 5 A_NoBlocking
		GO99 RSTUV 5
		GO99 W -1
		Stop
	Raise:
		GO99 NMLKJIH 7
		Goto See
	}
}
ACTOR VileGunGuy
{
	Health 20
	Radius 20
	Height 56
	Speed 8
	PainChance 200
	Monster
	+FLOORCLIP
	SeeSound "grunt/sight"
	AttackSound "grunt/attack"
	PainSound "grunt/pain"
	DeathSound "grunt/death"
	ActiveSound "grunt/active"
	Obituary "%o was killed by a zombieman."
	DropItem "Clip"
	States
	{
	Spawn:
		PBAK AB 10 A_Look
		Loop
	See:
		PBAK AABBCCDD 4 A_Chase
		Loop
	Missile:
		PBAK A 1 A_Jump(255,"Missile1", "Missile2")
		Goto See
	Missile1:
		PBAK E 10 A_FaceTarget
		PBAK F 8 A_PosAttack
		PBAK E 8
		PBAK E 10 A_CPosRefire
		Goto Missile1+1
	Missile2:
		PBAK E 20 A_FaceTarget
		PBAK F 10 A_CustomMissile("EnemyWakeMissile")
		PBAK E 10
		Goto See
	Pain:
		PBAK G 3
		PBAK G 3 A_Pain
		Goto See
	Death:
		PBAK H 5
		PBAK I 5 A_Scream
		PBAK J 5 A_NoBlocking
		PBAK K 5
		PBAK L -1
		Stop
	XDeath:
		PBAK M 5
		PBAK N 5 A_XScream
		PBAK O 5 A_NoBlocking
		PBAK PQRST 5
		PBAK U -1
		Stop
	Raise:
		PBAK K 5
		PBAK JIH 5
		Goto See
	}
}
ACTOR EnemyWakeMissile : WakeMissile { -FRIENDLY }
ACTOR Appeared : Inventory { Inventory.MaxAmount 1 }
ACTOR EnemySentryGun : SentryGun
{
	Scale 0.8
	Health 210
	-FRIENDLY
	states
	{
	Missile:
		SENT A 5 A_FaceTarget
		SENT B 5 bright A_CPosAttack
		SENT A 0 A_CposRefire
		goto Missile+1
	}
}
ACTOR EnemyRocketSentryGun : RocketSentryGun 
{
	Scale 0.8
	Health 210
	-FRIENDLY
	states
	{
	Missile:
		RENT A 25 A_FaceTarget
		RENT B 15 bright A_CustomMissile("MiniRocket",39,0,0,0)
		RENT A 0 A_CposRefire
	}
}
ACTOR MiniRocket : AMissile { Scale 0.35 }
ACTOR EnemyPlasmaSentryGun : PlasmaSentryGun 
{
	Scale 0.8
	Health 210
	-FRIENDLY
	states
	{
	Missile:
		PENT A 5 A_FaceTarget
		PENT B 3 bright A_BspiAttack
		PENT A 0 A_SpidRefire
		goto Missile+1
	}
}
ACTOR IceZombieMan : ZombieMan
{
	DamageType Ice
	Translation Ice
}
ACTOR ZombieMan_Translation1 : ZombieMan
{
	Translation 1
}
ACTOR ZombieMan_Translation2 : ZombieMan
{
	Translation 2
}
ACTOR IceChaingunGuy : ChaingunGuy
{
	DamageType Ice
	Translation Ice
}
ACTOR ChaingunGuy_Translation1 : ChaingunGuy
{
	Translation 1
}
ACTOR ChaingunGuy_Translation2 : ChaingunGuy
{
	Translation 2
}
ACTOR IceDemon : Demon
{
	DamageType Ice
	Translation Ice
}
ACTOR Demon_Translation1 : Demon
{
	Translation 1
}
ACTOR Demon_Translation2 : Demon
{
	Translation 2
}
ACTOR IceDoomImp : DoomImp
{
	DamageType Ice
	Translation Ice
}
ACTOR DoomImp_Translation1 : DoomImp
{
	Translation 1
}
ACTOR DoomImp_Translation2 : DoomImp
{
	Translation 2
}
ACTOR RaisingArchvile
{
	Health 700
	Radius 20
	Height 56
	Mass 500
	Speed 15
	PainChance 10
	Monster
	MaxTargetRange 896
	+QUICKTORETALIATE
	+FLOORCLIP
	+NOTARGET
	SeeSound "vile/sight"
	PainSound "vile/pain"
	DeathSound "vile/death"
	ActiveSound "vile/active"
	MeleeSound "vile/stop"
	Obituary "$OB_VILE"
	States
	{
	Spawn:
		VILE AB 10 A_Look
		Loop
	See:
		VILE AABBCCDDEEFF 2 A_VileChase
		Loop
	Missile:
		VILE G 0 Bright A_VileStart
		VILE G 10 Bright A_FaceTarget
		VILE H 8 Bright A_VileTarget
		VILE IJKLMN 8 Bright A_FaceTarget
		VILE O 8 Bright A_VileAttack
		VILE P 20 Bright
		Goto See
	Heal:
		VILE "[\]" 10 Bright
		Goto See
	Pain:
		VILE Q 5   
		VILE Q 5 A_Pain
		Goto See
	Death:
		VILE Q 7
		VILE R 7 A_Scream
		VILE S 7 A_NoBlocking
		VILE TUVWXY 7
		VILE Z -1
		Stop
	Raise:
		VILE ZYXWVUTSRQ 7
		Goto See
	}
}
Actor BloodArchvile : Archvile
{
	Health 550
	Translation "128:151=176:191"
}
ACTOR RaisingCyberdemon
{
	Health 4000
	Radius 40
	Height 110
	Mass 1000
	Speed 16
	PainChance 20
	Monster
	MinMissileChance 160
	+BOSS
	+MISSILEMORE
	+FLOORCLIP
	+NORADIUSDMG
	+DONTMORPH
	+BOSSDEATH
	SeeSound "cyber/sight"
	PainSound "cyber/pain"
	DeathSound "cyber/death"
	ActiveSound "cyber/active"
	Obituary "$OB_CYBORG"
	States
	{
	Spawn:
		CYBR AB 10 A_Look
		Loop
	See:
		CYBR A 3 A_Hoof
		CYBR ABBCC 3 A_Chase
		CYBR D 3 A_Metal
		CYBR D 3 A_Chase
		Loop
	Missile:
		CYBR E 6 A_FaceTarget
		CYBR F 12 A_CyberAttack
		CYBR E 12 A_FaceTarget
		CYBR F 12 A_CyberAttack
		CYBR E 12 A_FaceTarget
		CYBR F 12 A_CyberAttack
		Goto See
	Pain:
		CYBR G 10 A_Pain
		Goto See
	Death:
		CYBR H 10
		CYBR I 10 A_Scream
		CYBR JKL 10
		CYBR M 10 A_NoBlocking
		CYBR NO 10
		CYBR P 30
		CYBR P -1 A_BossDeath
		Stop
	Raise:
		CYBR PONMLKJIH 10
		Goto See
	}
}
ACTOR MiniCyberdemon
{
	Health 2000
	Radius 20
	Height 56
	Scale 0.5
	Mass 500
	Speed 24
	PainChance 40
	Monster
	MinMissileChance 160
	+BOSS
	+MISSILEMORE
	+FLOORCLIP
	+NORADIUSDMG
	+DONTMORPH
	+BOSSDEATH
	SeeSound "cyber/sight"
	PainSound "cyber/pain"
	DeathSound "cyber/death"
	ActiveSound "cyber/active"
	Obituary "$OB_CYBORG"
	States
	{
	Spawn:
		CYBR AB 10 A_Look
		Loop
	See:
		CYBR A 3 A_Hoof
		CYBR ABBCC 3 A_Chase
		CYBR D 3 A_BabyMetal
		CYBR D 3 A_Chase
		Loop
	Missile:
		CYBR E 6 A_FaceTarget
		CYBR F 12 A_SkelMissile
		CYBR E 12 A_FaceTarget
		CYBR F 12 A_SkelMissile
		CYBR E 12 A_FaceTarget
		CYBR F 12 A_SkelMissile
		Goto See
	Pain:
		CYBR G 10 A_Pain
		Goto See
	Death:
		CYBR H 10
		CYBR I 10 A_Scream
		CYBR JKL 10
		CYBR M 10 A_NoBlocking
		CYBR NO 10
		CYBR P 30
		CYBR P -1 A_BossDeath
		Stop
	Raise:
		CYBR PONMLKJIH 10
		Goto See
	}
}
ACTOR FT_ZombieMan : ZombieMan
{
	PainChance 255
	States
	{
	Pain.SmallStun:
		"####" "#" 3
		"####" "#" 3 A_Pain
		Goto See
	}
}
ACTOR FT_ChaingunGuy : ChaingunGuy {}
ACTOR FT_Demon : Demon {}
ACTOR FT_DoomImp : DoomImp {}
ACTOR FT_Archvile : Archvile {}
ACTOR FT_Cyberdemon : Cyberdemon {}