Actor FTMedic : DoomPlayer 10003
{
	Player.DisplayName "Medic"
	Player.WeaponSlot 1, Melee
	Player.StartItem "IsMedic"
	Player.StartItem Melee
	
	States
	{
	Spawn:
		PBAK A -1
		Loop
	See:
		PBAK ABCD 4 
		Loop
	Missile:
		PBAK E 12 
		Goto Spawn
	Melee:
		PBAK F 6 BRIGHT
		Goto Missile
	Pain:
		PBAK G 4 
		PBAK G 4 A_Pain
		Goto Spawn
	Death:
		PBAK H 10
		PBAK I 10 A_PlayerScream
		PBAK J 10 A_NoBlocking
		PBAK KLM 10
		PBAK N -1
		Stop
	XDeath:
		PBAK O 5
		PBAK P 5 A_XScream
		PBAK Q 5 A_NoBlocking
		PBAK RSTUV 5
		PBAK W -1
		Stop
	}
}

Actor IsMedic : Inventory { Inventory.MaxAmount 1 }
Actor TossStimpack : Inventory { Inventory.MaxAmount 1 }
Actor TossMedikit : Inventory { Inventory.MaxAmount 1 }

Actor MedicStimpack : Health
{
	Radius 8
	Height 16
	Mass 100
	Inventory.Amount 15
	Inventory.PickupMessage "$GOTSTIM"
	
	States
	{
	Spawn:
		STIM A -1
		Stop
	}
}

Actor ThrownStimpack
{
	Radius 8
	Height 16
	Mass 100
	Damage 1
	DamageType SmallStun
	Speed 15
	
	+MISSILE
	
	States
	{
	Spawn:
		STIM A 1
		Loop
	Death:
		STIM A 0
		STIM A 0 A_DropItem("MedicStimpack")
		Stop
	}
}

Actor MedicStimpackItem : CustomInventory
{
	Inventory.MaxAmount 3
	Inventory.PickupMessage "Got a spare stimpack."
	Inventory.Icon "STIMA0"
	
	+INVENTORY.INVBAR
	+INVENTORY.UNTOSSABLE
	
	States
	{
	Use:
		NULL Z 0 A_GiveInventory("TossStimpack", 1)
		Stop
	}
}

Actor MedicMedikit : Health
{
	Radius 14
	Height 20
	Mass 150
	Inventory.Amount 40
	Inventory.PickupMessage "$GOTMEDIKIT"
	
	States
	{
	Spawn:
		MEDI A -1
		Stop
	}
}

Actor ThrownMedikit
{
	Radius 14
	Height 20
	Mass 150
	Damage 1
	DamageType SmallStun
	Speed 10
	
	+MISSILE
	
	States
	{
	Spawn:
		MEDI A 1
		Loop
	Death:
		MEDI A 0
		MEDI A 0 A_DropItem("MedicMedikit")
		Stop
	}
}

Actor MedicMedikitItem : CustomInventory
{
	Inventory.MaxAmount 1
	Inventory.PickupMessage "Got a spare medikit."
	Inventory.Icon "MEDIA0"
	
	+INVENTORY.INVBAR
	+INVENTORY.UNTOSSABLE
	
	States
	{
	Use:
		NULL Z 0 A_GiveInventory("TossMedikit", 1)
		Stop
	}
}