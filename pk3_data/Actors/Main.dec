ACTOR NewTeleportFog : TeleportFog replaces TeleportFog
{
	Scale 0.9
	RenderStyle Add
	States
	{
	Spawn:
		TELP ABCDEF 5 Bright
		Stop
	}
}
ACTOR SSG : Supershotgun replaces SuperShotgun
{
	Weapon.AmmoUse 2
	Weapon.AmmoGive 8
	Weapon.AmmoType "Shell"
	Inventory.PickupMessage "You got the super shotgun!" 
	Obituary "%o was splattered by %k's super shotgun."
	States
	{
	Ready:
		SHT2 A 1 A_WeaponReady
		Loop
	Deselect:
		SHT2 A 1 A_Lower
		Loop
	Select:
		SHT2 A 1 A_Raise
		Loop
	Fire:
		SHT2 A 3 
		SHT2 A 21 A_FireShotgun2
		SHT2 B 4
		SHT2 C 4 A_CheckReload
		SHT2 D 4 A_OpenShotgun2
		SHT2 E 4
		SHT2 F 5 A_LoadShotgun2
		SHT2 G 4
		SHT2 H 3 A_CloseShotgun2
		SHT2 A 3 A_ReFire
		Goto Ready
	Flash:
		SHT2 I 4 Bright A_Light1
		SHT2 J 3 Bright A_Light2
		Goto LightDone
	Spawn:
		WP32 A -1
		Stop
	}
}
ACTOR isfighter : inventory
{
	Inventory.MaxAmount 1 
}
ACTOR isEngineer : inventory 
{
	Inventory.MaxAmount 1 
}
ACTOR isHealer : inventory 
{
	Inventory.MaxAmount 1 
}
ACTOR Bazooka : Rocketlauncher
{
	states
	{
	Ready:
		MISG A 1 A_WeaponReady
		Loop
	Deselect:
		MISG A 1 A_Lower
		Loop
	Select:
		MISG A 1 A_Raise
		Loop
	Fire:
		MISG B 8 A_GunFlash
		MISG B 12 A_FireMissile
		MISG B 30
		Goto Ready
	Flash:
		MISF A 3 Bright A_Light1
		MISF B 4 Bright
		MISF CD 4 Bright A_Light2
		Goto LightDone
	Spawn:
		WP51 A -1
		Stop
	}
}
ACTOR Pumped : inventory
{
	Inventory.MaxAmount 1
}
ACTOR AMissile : rocket replaces Rocket
{
	Decal BigScorch
	+ROCKETTRAIL
	Speed 39
	states
	{
	Spawn:
		MISL A 5 A_CustomMissile("RevenantTracerSmoke1",0,random(-60,60),0,0)
		Loop
	Death:
		EXS1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("Tracerpiece",3,0,random(0,360),2,random(0,360))
		EXS1 A 2 bright A_Explode//(112,145)
		EXS1 A 0 A_SpawnItemEx("AmissileExplosion",0)
		EXS1 GH 2 bright A_FADEOUT(0.1)
		Stop
	}
}
ACTOR AMissileExplosion 
{
    Radius 0.0001
	Height 0.0001
	RenderStyle Add
	+NOGRAVITY
	+NOINTERACTION
	+NOTDMATCH
	states
	{
	Spawn:
		EXS1 ABCDEF 2 bright 
		EXS1 GH 2 bright A_FADEOUT(0.1)
		Stop
	}
}
ACTOR Tracerpiece : RevenantTracerSmoke
{
	-NOGRAVITY
	Radius 5
	height 5
	Speed 5
	Gravity 0.05
	states
	{
	Spawn:
		SMOK A 5 bright A_FadeOut(0.1)
		Stop
	}
}
ACTOR RevenantTracerSmoke1 : RevenantTracerSmoke replaces RevenantTracerSmoke
{
	Scale 0.35
	Speed 5
	states
	{
	Spawn:
		SMOK A 5 bright A_FadeOut(0.1)
		Loop
	}
}
ACTOR AntiTechGrenade : CustomInventory 
{
	Inventory.Icon "INV_AAA"
	Inventory.Amount 1
	Inventory.MaxAmount 15
	Inventory.PickupMessage "picked up an antitech grenade"
	Scale 0.5
	+INVBAR
	+FLOORCLIP
	+WINDTHRUST
	States
	{
	Spawn:
		THRW A -1
		Stop
	Use:
		THRW A 0 A_ThrowGrenade("ElectricGrenade",5)
		Stop
	} 
}
ACTOR RocketBox1 : RocketBox replaces RocketBox 
{
	Game Doom
	SpawnID 141
	Inventory.PickupMessage "picked up a rocket box"
	States
	{
	Spawn:
		BROK A -1
		Stop
	Pickup:
		BROK A 0 A_GiveInventory("RocketAmmo",5)
		BROK A 1 A_GiveInventory("AntiTechGrenade",1)
		Stop
	}
}
ACTOR ElectricGrenade
{
	Radius 8
	Height 8
	Speed 12
	Damage -1
	PROJECTILE
	-NOGRAVITY
	-DROPOFF
	+DOOMBOUNCE
	+FRIENDLY
	DamageType "ElectricBomb"
	DeathSound "Sparks/Spawn"
	Scale 0.7
	States
	{
	Spawn: 
		THRW ABCDEF 3 bright
		Loop
	Death:
		FX16 G 1 A_MushRoom("TurretAzul",4)
		FX16 GHIJK 6 bright
		Stop
	}
}
ACTOR TurretAzul
{
	Radius 3
	Scale 0.85
	Height 3
	Speed 12
	Damage 1
	PROJECTILE
	DamageType "ElectricBomb"
	States
	{
	Spawn: 
		FX16 DE 3 bright
		Loop
	}
}
ACTOR Hands : Weapon 
{
	Weapon.KickBack 12
	Inventory.PickupMessage ""
	Obituary "%k > Unarmed > %o"
	+WEAPON.NOAUTOFIRE
	+INVENTORY.UNDROPPABLE
	+NOALERT
	States
	{
	Ready:
		HAND A 0 A_PlaySound("KNUCKLED")
		HAND ABCD 4
		PUNG A 1 A_WeaponReady
		PUNG A 1 A_WeaponReady
		Goto Ready+5
	Deselect:
		PUNG L 0 A_Lower
		PUNG L 1 A_Lower
		Loop
	Select:
		PUNG A 0 A_Raise
		PUNG A 1 A_Raise
		Loop
	Fire:
		PUNG B 1 A_PlayWeaponSound("SWING")
		PUNG CD 2 
		PUNG D 1 A_CustomPunch (3, 0, 0, "BulletPuff")
		PUNG DC 2 
		Goto Ready+5
	AltFire:
		PUNG E 1 A_PlayWeaponSound("SWING")
		PUNG FG 2 
		PUNG G 1 A_CustomPunch (3, 0, 0, "BulletPuff")
		PUNG GF 2 
		Goto Ready+5
	}
}
ACTOR StrikeCount : inventory
{
	Inventory.MaxAmount 1 
}
ACTOR SingleMarineBOT_SpawnerItem : CustomInventory 
{
	Inventory.Icon "INV_BBB"
	Inventory.Amount 1
	Inventory.MaxAmount 15
	Scale 0.5
	+INVBAR
	+FLOORCLIP
	+WINDTHRUST
	States
	{
	Use:
		TLP2 A 0 A_ThrowGrenade("SingleMarineBOT_Spawner",5)
		Stop
	} 
}
ACTOR SingleMarineBOT_Spawner
{
	Radius 5
	Height 5 
	scale 0.35
	states
	{
	Spawn:
		TLP2 A 4 
		TLP2 A 50 bright
		TLP2 A 14 A_SpawnItemEx("MarineNPC",0,0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TLP2 A 5 A_FadeOut(0.1)
		Stop
	}
}
ACTOR MarineNPC
{
	Health 100
	Radius 16
	Height 56
	Mass 100
	Speed 12
	PainChance 160
	Monster
	-COUNTKILL
	+FRIENDLY
	+CANUSEWALLS
	+CANPUSHWALLS
	Translation 0
	Damage 100
	DeathSound "player*death"
	PainSound "player*pain50"
	AttackSound "Weapons/MP5KFIR"
	States
	{
	Spawn:
		PLAY A 4 A_Look
		PLAY A 4 A_Wander
		Loop
	Idle:
		PLAY A 4 A_Look
		PLAY A 4 A_Wander
		PLAY A 4 A_Look
		PLAY B 4 A_Look
		PLAY B 4 A_Wander
		PLAY B 4 A_Look
		Loop
	See:
		PLAY ABCD 4 A_Chase
		Loop
	Melee:
		PLAY E 4 
		PLAY E 4 A_M_Saw
		PLAY E 0 A_JumpIfCloser(40, "Melee")
		Goto See
	Missile:
		PLAY E 4 A_FaceTarget
		PLAY FF 4 Bright A_CPosAttack
		PLAY FF 4 Bright A_CPosAttack
		PLAY A 0 A_CPosRefire
		Goto Missile+3
	Pain:
		PLAY G 4
		PLAY G 4 A_Pain
		Goto Idle
	Death:
		PLAY H 10
		PLAY I 10 A_Scream
		PLAY J 10 A_NoBlocking
		PLAY KLM 10
		PLAY N -1
		Stop
	XDeath:
		PLAY O 5
		PLAY P 5 A_XScream
		PLAY Q 5 A_NoBlocking
		PLAY RSTUV 5
		PLAY W -1
		Stop
	Raise:
		PLAY MLKJIH 5
		Goto See
	}
}
ACTOR MarineNPC_Evil : MarineNPC
{
	-FRIENDLY
}