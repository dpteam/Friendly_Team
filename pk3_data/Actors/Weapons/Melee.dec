Actor Melee : Weapon
{
	Weapon.SlotNumber 1
//	Weapon.SelectionOrder 3700
	Weapon.Kickback 100
	+WEAPON.MELEEWEAPON
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	Obituary "%o was punched to death by %k"
	
	States
	{
	Ready:
/*		FIST A 0 A_JumpIfInventory("TossStimpack", 1, "TossStimpack")
		FIST A 0 A_JumpIfInventory("TossMedikit", 1, "TossMedikit")*/
		FIST A 1 A_WeaponReady
		Loop
	Deselect:
		FIST A 1 A_Lower
		Loop
	Select: 
		FIST A 1 A_Raise
		Loop
	Fire:
		FIST A 1
		FIST A 1 Offset(0, 48) //50 118
		FIST A 1 Offset(0, 66)
		TNT1 A 12
		NULL Z 0 A_PlaySound("FIST/SWING")
		NULL Z 0 A_JumpIfInventory("FistToken", 1, "LeftPunch")
	RightPunch:
		NULL Z 0 A_GiveInventory("FistToken", 1)
		FIST B 1 Offset(74, 84) //161 95
		FIST B 1 Offset(57, 64)
		FIST C 1 Offset(41, 55) //143 84
		FIST C 1 Offset(-6, 40)
		FIST D 1 Offset(-45, 52) //121 60
		NULL Z 0 A_JumpIfInventory("PowerStrength", 1, "RightBerserk")
		FIST D 1 Offset(-107, 52) A_CustomPunch( 2 * random(1, 10), TRUE )
		Goto RightFinish
	RightBerserk:
		FIST D 1 Offset(-107, 52) A_CustomPunch( 20 * random(1, 10), TRUE )
	RightFinish:
		FIST D 1 Offset(-153, 66)
		FIST D 1 Offset(-200, 98)
		TNT1 A 12 A_WeaponReady( WRF_NOFIRE | WRF_NOSWITCH ) //Reset sprite offset
		NULL Z 0 A_ReFire
		FIST A 1 Offset(0, 66)
		FIST A 1 Offset(0, 48)
		FIST A 1 Offset(0, 32)
		Goto Ready
	LeftPunch:
		NULL Z 0 A_TakeInventory("FistToken", 1)
		FIST E 1 Offset(-66, 74) //35 104
		FIST E 1 Offset(-51, 59)
		FIST F 1 Offset(-55, 51) //48 87
		FIST F 1 Offset(-15, 37)
		FIST G 1 Offset(38, 52) //38 60
		NULL Z 0 A_JumpIfInventory("PowerStrength", 1, "LeftBerserk")
		FIST G 1 Offset(102, 58) A_CustomPunch( 2 * random(1, 10), TRUE )
		Goto LeftFinish
	LeftBerserk:
		FIST G 1 Offset(102, 58) A_CustomPunch( 20 * random(1, 10), TRUE )
	LeftFinish:
		FIST G 1 Offset(176, 69)
		FIST G 1 Offset(208, 87)
		TNT1 A 12 A_WeaponReady( WRF_NOFIRE | WRF_NOSWITCH )
		NULL Z 0 A_ReFire
		FIST A 1 Offset(0, 66)
		FIST A 1 Offset(0, 48)
		FIST A 1 Offset(0, 32)	
		Goto Ready 
	Hold:
		NULL Z 0 A_JumpIfInventory("FistToken", 1, "LeftPunch")
		Goto RightPunch
	AltFire:
		FIST A 1
		FIST A 1 Offset(0, 48) //50 118
		FIST A 1 Offset(0, 66)
		NULL Z 0 SetPlayerProperty(0, 1, 0)
		TNT1 A 12
		NULL Z 0 A_PlaySound("FIST/SWING")
		KICK A 1 Offset(68, 62) //34 130
		KICK A 1 Offset(59, 49)
		KICK A 1 Offset(40, 33)
		KICK A 2 Offset(29, 31)
		NULL Z 0 A_JumpIfInventory("PowerStrength", 1, "KickBerserk")
		KICK B 1 Offset(38, 36) A_CustomPunch( 3 * random(1, 10), TRUE )
		Goto KickFinish
	KickBerserk:
		KICK B 1 Offset(38, 36) A_CustomPunch( 30 * random(1, 10), TRUE )
	KickFinish:
		KICK B 6 Offset(36, 32) //38 44
		KICK B 1 Offset(40, 37)
		KICK A 1 Offset(43, 31) 
		KICK A 1 Offset(52, 42) 
		KICK A 1 Offset(65, 58)
		TNT1 A 16 A_WeaponReady( WRF_NOFIRE | WRF_NOSWITCH )
		NULL Z 0 SetPlayerProperty(0, 0, 0)
		FIST A 1 Offset(0, 66)
		FIST A 1 Offset(0, 48)
		FIST A 1 Offset(0, 32)
		Goto Ready
/*	TossStimpack:
		NULL Z 0 A_FireCustomMissile ("ThrownStimpack", 0, 0, 0, 0, 0, 6)
		NULL Z 0 A_TakeInventory("TossStimpack", 1)
		Goto Ready
	TossMedikit:
		NULL Z 0 A_FireCustomMissile ("ThrownMedikit", 0, 0, 0, 0, 0, 5)
		NULL Z 0 A_TakeInventory("TossMedikit", 1)
		Goto Ready*/
	} 
}

actor FistToken: Ammo 
{
	Inventory.MaxAmount 1
}
